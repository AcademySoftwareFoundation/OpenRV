#
# Copyright (C) 2022  Autodesk, Inc. All Rights Reserved.
#
# SPDX-License-Identifier: Apache-2.0
#

MESSAGE(STATUS "Install prefix: \"${CMAKE_INSTALL_PREFIX}\"")
IF(NOT IS_ABSOLUTE ${CMAKE_INSTALL_PREFIX})
  MESSAGE(FATAL_ERROR "${CMAKE_INSTALL_PREFIX} is not an absolute path")
ENDIF()

FILE(
  GLOB_RECURSE FILES_TO_COPY
  LIST_DIRECTORIES FALSE
  RELATIVE ${RV_APP_ROOT}
  ${RV_APP_ROOT}/*
)

LIST(LENGTH FILES_TO_COPY FILES_TO_COPY_LENGTH)

SET(CURRENT_FILE_INDEX
    "0"
)
SET(FILES_TO_FIX_RPATH "")

FOREACH(
  FILE_TO_COPY
  ${FILES_TO_COPY}
)
  MATH(
    EXPR CURRENT_FILE_INDEX "${CURRENT_FILE_INDEX}+1"
    OUTPUT_FORMAT DECIMAL
  )

  MATH(
    EXPR CURRENT_PERCENTAGE " ${CURRENT_FILE_INDEX} * 100 / ${FILES_TO_COPY_LENGTH} "
    OUTPUT_FORMAT DECIMAL
  )
  BEFORE_COPY(${RV_APP_ROOT}/${FILE_TO_COPY} SHOULD_INSTALL)

  IF(NOT SHOULD_INSTALL)
    MESSAGE(STATUS "${CURRENT_PERCENTAGE}% -- Skipping\t ${RV_APP_ROOT}/${FILE_TO_COPY}")
    CONTINUE()
  ENDIF()

  GET_FILENAME_COMPONENT(DESTINATION_FOLDER ${CMAKE_INSTALL_PREFIX}/${FILE_TO_COPY} DIRECTORY)
  MESSAGE(STATUS "${CURRENT_PERCENTAGE}% -- Installing ${CMAKE_INSTALL_PREFIX}/${FILE_TO_COPY}")

  FILE(
    COPY ${RV_APP_ROOT}/${FILE_TO_COPY}
    DESTINATION ${DESTINATION_FOLDER}
    USE_SOURCE_PERMISSIONS
  )

  AFTER_COPY(${CMAKE_INSTALL_PREFIX}/${FILE_TO_COPY} ${RV_APP_ROOT}/${FILE_TO_COPY} FILES_TO_FIX_RPATH)
ENDFOREACH()

POST_INSTALL()